# -*- mode: shell-script; -*-

# History
HISTFILE=$HOME/.config/zsh/histfile
HISTSIZE=1000
SAVEHIST=1000

setopt autocd extendedglob nomatch notify
unsetopt beep
bindkey -e

# Dir History
setopt autopushd pushdminus pushdsilent pushdtohome
alias dh='dirs -v'

# X clipboard integration for kill / yank functions.
wl-copy-region-as-kill () {
    zle copy-region-as-kill
    [[ $DISPLAY ]] && echo -n $CUTBUFFER | wl-copy
}

wl-kill-region () {
    zle kill-region
    [[ $DISPLAY ]] && echo -n $CUTBUFFER | wl-copy
}

wl-kill-line () {
    zle kill-line
    [[ $DISPLAY ]] && echo -n $CUTBUFFER | wl-copy
}

wl-yank () {
    [[ $DISPLAY ]] && CUTBUFFER=$(wl-paste </dev/null)
    zle yank
}

# Add X integrated modules to the zsh line editor.
zle -N wl-copy-region-as-kill
zle -N wl-kill-region
zle -N wl-kill-line
zle -N wl-yank

# Bind keys in the line editor to the correct modules.
bindkey -e '\ew' wl-copy-region-as-kill
bindkey -e '^W' wl-kill-region
bindkey -e '^K' wl-kill-line
bindkey -e '^Y' wl-yank

# Better word separation
autoload -U select-word-style
select-word-style bash

# alias
alias ls="exa --color=always --group-directories-first"
alias ll="exa -lah --color=always --group-directories-first"

# direnv
eval "$(direnv hook zsh)"

# Prompt
fpath+=$HOME/.guix-home/profile/share/zsh/plugins/pure
autoload -Uz promptinit; promptinit
prompt pure

TYPEWRITTEN_PROMPT_LAYOUT="pure"

# autosuggestions
source $HOME/.guix-home/profile/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh

# misc
fpath+=$HOME/.guix-home/profile/share/zsh/site-functions

# completion
autoload -Uz compinit
compinit

zstyle ':completion:*' menu select

# Vterm Integration
vterm_printf() {
    if [ -n "$TMUX" ] && ([ "${TERM%%-*}" = "tmux" ] || [ "${TERM%%-*}" = "screen" ] ); then
        # Tell tmux to pass the escape sequences through
        printf "\ePtmux;\e\e]%s\007\e\\" "$1"
    elif [ "${TERM%%-*}" = "screen" ]; then
        # GNU screen (screen, screen-256color, screen-256color-bce)
        printf "\eP\e]%s\007\e\\" "$1"
    else
        printf "\e]%s\e\\" "$1"
    fi
}

vterm_prompt_end() {
    vterm_printf "51;A$(whoami)@$(hostname):$(pwd)";
}

setopt PROMPT_SUBST
PROMPT=$PROMPT'%{$(vterm_prompt_end)%}'
